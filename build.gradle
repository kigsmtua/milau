apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'application'
apply plugin: 'jacoco'

group = 'io.github.kigsmtua'
version = '0.1.0'
archivesBaseName = "milau"
description = """A distributed task queue supporting priorities and time based exection based on redis"""
sourceCompatibility = 1.8
targetCompatibility = 1.8
mainClassName = 'com.milau.cli.App'


repositories {
    mavenCentral()
}

dependencies {
    compile group: 'redis.clients', name: 'jedis', version: '2.9.0'
    compile group: 'ch.qos.logback', name: 'logback-core', version: '1.2.3'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.6'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.6'
    compile group: 'co.paralleluniverse', name: 'quasar-core', version: '0.7.5'
    compile group: 'com.beust', name: 'jcommander', version: '1.72'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile 'com.google.guava:guava:23.0'
    testCompile 'junit:junit:4.12'
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                   'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                   'Main-Class': 'com.milau.cli.App',
                   'Implementation-Version': project.version)
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport

test {
    testLogging.showStandardStreams = true
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}


publishing{
    publications{
        mavenJava(MavenPublication){
            artifactId = 'Milau'
            from components.java
            artifact sourceJar
            artifact javadocJar
            pom{
                name = 'Milau'
                description ='A distributed task queue supporting priorities and time based execution based on redis'
                url ='https://github.com/kigsmtua/milau'
           
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/kigsmtua/milau/blob/master/LICENSE'
                        distribution = 'repo'
                    }
                }
                scm {
                    url = 'https://github.com/kigsmtua/milau'
                    connection = 'scm:git:git://github.com/kigsmtua/milau.git'
                    developerConnection = 'scm:git:ssh://git@github.com:kigsmtua/milau.git'
                }
                developers {
                    developer {
                        id = 'kigsmtua'
                        name = 'John Kiragu'
                        email = 'mutuakiragu@gmail.com'
                    }
                }
            }
        }
    }
//    repositories {
//        maven {
//            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
//            credentials {
//                username = ossrhUsername
//                password = ossrhPassword
//            }
//        }
//    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html4', true)
    }
}